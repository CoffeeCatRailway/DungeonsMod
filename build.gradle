buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

version = "${project.version}-${project.mcVersion}"
group = "${project.group}.${project.modId}"
archivesBaseName = project.modId

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: project.mcpChannel.toString(), version: project.mcpMappings.toString()
    
    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            mods {
                dungeonsmod {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            mods {
                dungeonsmod {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            args '--mod', 'dungeonsmod', '--all', '--output', '"' + rootProject.file('src/generated/resources/') + '"', '--existing', '"' + rootProject.file('src/main/resources') + '"'

            mods {
                dungeonsmod {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'

//    exclude "**/*.ase"
}

configurations {
    shade
}

repositories {
    jcenter()
    mavenLocal()

    maven { // Curios
        url = "https://maven.theillusivec4.top/"
    }
    maven { // JustEnoughItems
        url = "https://dvs1.progwml6.com/files/maven/"
    }

    maven { // Registrate
        name 'tterrag maven'
        url "https://maven.tterrag.com/"
    }
    maven { // FishLib
        name = "Jitpack"
        url = "https://jitpack.io"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${project.mcVersion}-${project.forgeVersion}"

    runtimeOnly fg.deobf("top.theillusivec4.curios:curios:FORGE-${project.mcVersion}-${project.curiosVersion}")
    compileOnly fg.deobf("top.theillusivec4.curios:curios:FORGE-${project.mcVersion}-${project.curiosVersion}:api")

    runtimeOnly fg.deobf("mezz.jei:jei-${project.mcVersion}:${project.jeiVersion}")
    compileOnly fg.deobf("mezz.jei:jei-${project.mcVersion}:${project.jeiVersion}:api")

    def registrate = "com.tterrag.registrate:Registrate:MC${project.mcVersion}-${project.registrateVersion}"
    implementation fg.deobf(registrate)
    shade registrate

    def fishLib = "com.github.Ocelot5836:FishLib:${project.mcVersion}-${project.fishLibVersion}"
    implementation fg.deobf(fishLib)
    shade fishLib
}

processResources {
    inputs.property("version", version)
    inputs.property("modId", project.modId)
    inputs.property("name", project.name)
    inputs.property("author", project.author)
    from(sourceSets.main.resources.srcDirs) {
        include 'META-INF/mods.toml'
        expand 'version': version, 'modId': project.modId, 'name': project.name, 'author': project.author
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'META-INF/mods.toml'
    }
}

shadowJar {
    configurations = [project.configurations.shade]
    relocate 'com.tterrag.registrate', 'coffeecatrailway.repack.registrate'
    relocate 'io.github.ocelot', 'io.github.ocelot.lib'
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "${archivesBaseName}",
                "Specification-Vendor"    : "${project.author}",
                "Specification-Version"   : "${project.version}",
                "Implementation-Title"    : "${project.name}",
                "Implementation-Version"  : "${project.version}",
                "Implementation-Vendor"   : "${project.author}",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
    enabled = false
    dependsOn(shadowJar { classifier = null })
}